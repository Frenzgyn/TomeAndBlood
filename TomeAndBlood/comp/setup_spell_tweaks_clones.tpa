// -----------------------------
// SPELL TWEAKS: ILLUSIONARY CLONES
// -----------------------------

// This component uses code by Subtledoctor and is used with permission of the original author. 


//updated illusionary clone spells___________________________________________________
//
LAF d5_resolve_state STR_VAR new_state_id = ~D5_MISLEAD~ RET new_state_ind END
OUTER_SET mislead_clone_state = %new_state_ind%

LAF d5_resolve_state STR_VAR new_state_id = ~D5_L_SIMULACRUM~ RET new_state_ind END
OUTER_SET lesser_simulacrum_state = %new_state_ind%

//LAF d5_resolve_state STR_VAR new_state_id = ~D5_SIMULACRUM~ RET new_state_ind END
//OUTER_SET simulacrum_state = %new_state_ind%

ACTION_IF FILE_EXISTS_IN_GAME ~spwi804.spl~ BEGIN

	OUTER_SET spell_ind = 0
	COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
	  READ_SHORT 0x1c spell_type
	  PATCH_IF (spell_type < 3) BEGIN
		READ_LONG 0x34 spell_level
		PATCH_IF (spell_level > 1) AND (spell_level < 5) BEGIN
		  SET spell_ind = (%spell_ind% + 1)
		  TEXT_SPRINT $d5_no_lowspells(~%spell_ind%~) ~%SOURCE_RES%~
		END
		PATCH_IF (spell_level > 4) AND (spell_level < 8) BEGIN
		  SET spell_ind = (%spell_ind% + 1)
		  TEXT_SPRINT $d5_no_midspells(~%spell_ind%~) ~%SOURCE_RES%~
		END
		PATCH_IF (spell_level > 7) BEGIN
		  SET spell_ind = (%spell_ind% + 1)
		  TEXT_SPRINT $d5_no_highspells(~%spell_ind%~) ~%SOURCE_RES%~
		END
	  END
	BUT_ONLY

//...apply penalties to simulacra
//
	COPY ~TomeAndBlood/data/spell_tweaks/simulacr.spl~ ~override~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 337 target = 1 parameter1 = ~-1~ parameter2 = 237	END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 144 target = 1 parameter2 = 9 timing = 1 END 	//	no item use
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 144 target = 1 parameter2 = 11 timing = 1 END 	//	no item use
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 144 target = 1 parameter2 = 12 timing = 1 END 	//	no item use
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %mislead_clone_state% parameter2 = 110 timing = 1 STR_VAR resource = ~d5simul6~ END // and make d5simul6 318 immune if stat = 3
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %lesser_simulacrum_state% parameter2 = 110 timing = 1 STR_VAR resource = ~d5simul7~ END // and make d5simul7 318 immune if stat = 3
//	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %simulacrum_state% parameter2 = 110 timing = 1 STR_VAR resource = ~d5simul8~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = ~d5simul8~ END

	COPY ~TomeAndBlood/data/core/d5_base.spl~ ~override/d5simul6.spl~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 278 target = 1 parameter1 = (0 - 7) parameter2 = 0 timing = 9 END 	//	-7 thac0
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 191 target = 1 parameter1 = (0 - 7) parameter2 = 0 timing = 9 END 	//	-7 arcane caster level
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 18 target = 1 parameter1 = 40 parameter2 = 2 timing = 9 END 		//	-60% hp
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 145 target = 1 parameter2 = 1 timing = 1 END 	//	no divine spellcasting
	  PHP_EACH d5_no_lowspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 2-3 spells
	  END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src2.spl~ END 		//	for spontaneous casters
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-2.spl~ END 	
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src3.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-3.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src4.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-4.spl~ END 
	  PHP_EACH d5_no_midspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 4-6 spells
	  END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src5.spl~ END 		//	for spontaneous casters
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-5.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src6.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-6.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src7.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-7.spl~ END 
	  PHP_EACH d5_no_highspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 7+ spells
	  END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src8.spl~ END 		//	for spontaneous casters
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-8.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src9.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-9.spl~ END 
//	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = (3 << 24) parameter2 = %clone_stat% timing = 9 STR_VAR resource = ~d5simul6~ END

	COPY ~TomeAndBlood/data/core/d5_base.spl~ ~override/d5simul7.spl~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 278 target = 1 parameter1 = (0 - 5) parameter2 = 0 timing = 9 END 	//	-5 thac0
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 191 target = 1 parameter1 = (0 - 5) parameter2 = 0 timing = 9 END 	//	-5 arcane caster level
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 18 target = 1 parameter1 = 60 parameter2 = 2 timing = 9 END 		//	-40% hp
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 145 target = 1 parameter2 = 1 timing = 1 END 	//	no divine spellcasting
	  PHP_EACH d5_no_midspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 4-6 spells
	  END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src5.spl~ END 		//	for spontaneous casters
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-5.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src6.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-6.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src7.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-7.spl~ END 
	  PHP_EACH d5_no_highspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 7+ spells
	  END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src8.spl~ END 		//	for spontaneous casters
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-8.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src9.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-9.spl~ END 
//	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = (3 << 24) parameter2 = %clone_stat% timing = 9 STR_VAR resource = ~d5simul7~ END

	COPY ~TomeAndBlood/data/core/d5_base.spl~ ~override/d5simul8.spl~
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 278 target = 1 parameter1 = (0 - 3) parameter2 = 0 timing = 9 END 	//	-3 thac0
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 191 target = 1 parameter1 = (0 - 3) parameter2 = 0 timing = 9 END 	//	-3 arcane caster level
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 18 target = 1 parameter1 = 75 parameter2 = 2 timing = 9 END 		//	-25% hp
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = ~spwi505~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = ~spwi607~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = ~spwi703~ END
	  PHP_EACH d5_no_highspells AS ind => spell BEGIN
		LPF ADD_SPELL_EFFECT INT_VAR opcode = 172 target = 1 timing = 9 STR_VAR resource = EVAL ~%spell%~ END 	//	remove level 7+ spells
	  END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src8.spl~ END 		//	for spontaneous casters
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-8.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src9.spl~ END 
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5src-9.spl~ END 
//	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = (3 << 24) parameter2 = %clone_stat% timing = 9 STR_VAR resource = ~d5simul8~ END
	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = %mislead_clone_state% parameter2 = 110 timing = 9 STR_VAR resource = ~d5simul8~ END
	  LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 318 target = 1 parameter1 = %lesser_simulacrum_state% parameter2 = 110 timing = 9 STR_VAR resource = ~d5simul8~ END

//...custom simulacrum summon
//
	COPY ~TomeAndBlood/data/spell_tweaks/d5si804.spl~ ~override~
	  SAY NAME1 ~ ~
	  SAY UNIDENTIFIED_DESC ~ ~

//...5th level: nerfed mislead (imp. invisibility, no super invisibility. and 50% hp)
//
	COPY_EXISTING ~spwi607.spl~ ~override~
	  READ_LONG 0x08 mislead_name
	  READ_LONG 0x50 mislead_desc
	BUT_ONLY
	COPY ~TomeAndBlood/data/spell_tweaks/spwi607.spl~ ~override/spwi505.spl~
	  WRITE_LONG 0x08 %mislead_name%
	  WRITE_LONG 0x50 %mislead_desc%
	  WRITE_LONG 0x34 5
	  WRITE_ASCII 0x3a ~spwi505c~ #8
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 5 STR_VAR icon = ~spwi505b~ END
	COPY ~TomeAndBlood/data/spell_tweaks/d5si505a.spl~ ~override~
	COPY ~TomeAndBlood/data/spell_tweaks/d5si505a.eff~ ~override~
	COPY ~TomeAndBlood/data/spell_tweaks/d5si505b.spl~ ~override~
	COPY ~TomeAndBlood/data/spell_tweaks/mislead.spl~ ~override~

//...6th level: shadow clone
//
	COPY_EXISTING ~spwi804.spl~ ~override/spwi607.spl~
	  SAY NAME1 @6271
	  SAY UNIDENTIFIED_DESC @6272
	  WRITE_LONG 0x34 6
	  LPF DELETE_EFFECT END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 1 parameter1 = 0 parameter2 = %mislead_clone_state% timing = 9 special = 1 END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5si804~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 4 duration = 2 STR_VAR resource = ~spwi607~ END
	  WRITE_ASCII 0x3a ~spwi607c~ #8
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 5 STR_VAR icon = ~spwi607b~ END
	ACTION_IF FILE_EXISTS_IN_GAME ~scrl7k.itm~ BEGIN
	  COPY_EXISTING ~scrl7k.itm~ ~override~
	  SAY NAME2 @6271
	  SAY IDENTIFIED_DESC @6272
	  WRITE_ASCII 0x3a ~spwi703a~ #8
	  LPF ALTER_ITEM_HEADER STR_VAR icon = ~spwi703a~ END
	END 

//...7th level: lesser simulacrum
	COPY_EXISTING ~spwi804.spl~ ~override/spwi703.spl~
	  SAY NAME1 @6273
	  SAY UNIDENTIFIED_DESC @6274
	  WRITE_LONG 0x34 7
	  LPF DELETE_EFFECT END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 1 parameter1 = 0 parameter2 = %lesser_simulacrum_state% timing = 9 special = 1 END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5si804~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = ~spwi206~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 4 duration = 22 STR_VAR resource = ~spwi703~ END
	  WRITE_ASCII 0x3a ~spwi703c~ #8
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 2 STR_VAR icon = ~spwi703b~ END

	ACTION_IF FILE_EXISTS_IN_GAME ~scrl8f.itm~ BEGIN
	  COPY_EXISTING ~scrl8f.itm~ ~override~
	  SAY NAME2 @6273
	  SAY IDENTIFIED_DESC @6274
	  WRITE_ASCII 0x3a ~spwi607a~ #8
	  LPF ALTER_ITEM_HEADER STR_VAR icon = ~spwi607a~ END
	END 

//...9th level: projected simulacrum
//
	COPY_EXISTING ~d5si804.spl~ ~override/d5si8049.spl~
	  LPF ALTER_SPELL_HEADER INT_VAR target = 1 END
	  LPF ALTER_EFFECT INT_VAR target = 2 END
	COPY_EXISTING ~spwi804.spl~ ~override/d5si9il.spl~
	  SAY NAME1 @6277
	  SAY UNIDENTIFIED_DESC @6278
	  WRITE_LONG 0x34 9
	  WRITE_ASCII 0x3a ~d5projic~ #8
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 5 target = 1 STR_VAR icon = ~d5projib~ END
	  LPF DELETE_EFFECT END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5si8049~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 2 timing = 4 duration = 2 STR_VAR resource = ~d5si9il~ END
	COPY ~TomeAndBlood/data/spell_tweaks/d5projic.bam~ ~override~
	COPY ~TomeAndBlood/data/spell_tweaks/d5projib.bam~ ~override~
	ADD_SPELL ~override/d5si9il.spl~ 2 9 D5_WIZARD_CLONE
	LAF RES_NUM_OF_SPELL_NAME
		STR_VAR spell_name = ~D5_WIZARD_CLONE~
		RET spell_res
	END
	ACTION_IF FILE_EXISTS_IN_GAME ~hidespl.2da~ BEGIN
		APPEND ~hidespl.2da~ ~%spell_res%	1		0~
	END

//...8th level: simulacrum
//
	COPY_EXISTING ~spwi804.spl~ ~override~
	  SAY NAME1 @6275
	  SAY UNIDENTIFIED_DESC @6276
	  LPF DELETE_EFFECT END
//	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 328 target = 1 parameter1 = 0 parameter2 = %simulacrum_state% timing = 9 special = 1 END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 duration = 0 STR_VAR resource = ~d5si804~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 1 parameter2 = 1 timing = 1 STR_VAR resource = ~spwi405~ END
	  LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 timing = 4 duration = 2 STR_VAR resource = ~spwi804~ END
	  LPF ALTER_SPELL_HEADER INT_VAR speed = 5 END
	ACTION_IF FILE_EXISTS_IN_GAME ~scrl8z.itm~ BEGIN
	  COPY_EXISTING ~scrl8z.itm~ ~override~
	  SAY NAME2 @6275
	  SAY IDENTIFIED_DESC @6276
	END 
END
//__________________________________________________________________________________


CLEAR_ARRAYS 
