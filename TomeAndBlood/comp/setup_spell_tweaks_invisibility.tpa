// -----------------------------
// SPELL TWEAKS: INVISIBILITY REVISIONS
// -----------------------------

// This component uses code by Subtledoctor and is used with permission of the original author. 


//check sanctuary for these tweaks__________________________________________________
//
 APPEND ~splprot.2da~ ~D5_SANCT%TAB%63%TAB%1%TAB%4~
 COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_SANCT~ BEGIN
	    SET d5_sanct_row = %row%
	  END
	END
 BUT_ONLY
//__________________________________________________________________________________


//change to invisibility/detect invisible___________________________________________
//
ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_insanctibility.d5~ BEGIN
  COPY_EXISTING ~sw1h01.itm~ ~override/d5_insanctibility.d5~ // detection for this tweak 

  COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 match_parameter2 = 1 opcode = 153 parameter2 = 1 STR_VAR resource = ~~ END
/*
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 match_parameter2 = 1 opcode = 66 parameter1 = 128 parameter2 = 0 STR_VAR insert = ~first~ END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 20 opcode = 177 target = 1 parameter2 = 2 parameter1 = 0 STR_VAR resource = ~d5sanct~ insert = ~last~ END 

Note: need to make subspell(s) to make translucency work with this... but they must use 321, and we don't know which spells to cancel.
So, here's the plan:
- make a list of spells and items that do invisibility
- patch them to add sanctuary, and translucency, and 177/232 casting spellZ
- SpellZ has 324 filtered by sanctuary = 1 (!! - need to switch the splprot entry relation)
- SpellZ then has 321 effects for each of the spells and items on the list.
*/
  BUT_ONLY
  
 ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN 	// no need to patch 203 if SR installed
  COPY_EXISTING ~spwi203.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 193 target = 1 STR_VAR resource = ~~ END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 136 duration = 1 END
 END
/*
  COPY_EXISTING ~spwi224.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 136 duration = 1 END 
*/
END
//__________________________________________________________________________________


//change sanctuary's visual effect__________________________________________________
//
 CREATE EFF ~d5pr109~
  WRITE_LONG 0x10 232
  WRITE_LONG 0x14 1
  WRITE_LONG 0x20 20
  WRITE_LONG 0x28 60
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~D5PR109A~ #8
  WRITE_LONG 0x48 102  
 COPY_EXISTING ~sppr109.spl~ ~override/d5pr109a.spl~
	LPF DELETE_EFFECT INT_VAR match_target = 1 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 target = 1 power = 0 parameter1 = 0 timing = 1 STR_VAR resource = ~SPPR109~ END
	LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 324 target = 1 power = 0 parameter2 = %d5_sanct_row% timing = 1 STR_VAR resource = EVAL ~%SOURCE_RES%~ END
 COPY_EXISTING ~sppr109.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 153 parameter2 = 1 STR_VAR resource = ~~ END
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 59 parameter1 = 25 parameter2 = 0 STR_VAR insert = ~first~ END 
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 275 parameter1 = 25 parameter2 = 0 STR_VAR insert = ~first~ END 
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 66 parameter1 = 128 parameter2 = 0 STR_VAR insert = ~first~ END 
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 153 opcode = 177 target = 1 parameter2 = 2 parameter1 = 0 STR_VAR resource = ~d5pr109~ insert = ~last~ END 
//__________________________________________________________________________________


//change SR Expeditious Retreat to Chameleon___________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN 	// version for Spell Revisions
 COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
	LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~SPWI108~ END
 BUT_ONLY

 COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	LPF ADD_ITEM_EFFECT INT_VAR type = 99 opcode = 321 target = 1 timing = 1 STR_VAR resource = ~SPWI108~ END
  END
 BUT_ONLY

 COPY_EXISTING ~spwi108.spl~ ~override/spwi108x.spl~
	LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
	LPF DELETE_EFFECT INT_VAR match_power = 1 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 21 power = 1 target = 1 parameter1 = 1 timing = 1 duration = 0 resist_dispel = 0 END
	LPF ALTER_SPELL_HEADER INT_VAR speed = 1 END
	WRITE_BYTE 0x19 (THIS BOR 0b00000100) // set "hostile" flag
	SAY NAME1 ~~
	SAY UNIDENTIFIED_DESC ~~

 COPY_EXISTING ~spwi108.spl~ ~override~
	WRITE_BYTE 0x1f (THIS BAND 0b11011111) 				// don't forbid transmuters
	WRITE_BYTE 0x1f (THIS BOR 0b00010000) 				// forbid necromancers
	WRITE_BYTE 0x25 5 									// illusion school
	WRITE_SHORT 0x22 13 								// illusion animation
	WRITE_ASCII 0x10 ~CAS_M01~ #8 						// illusion sounds
	LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
	LPF DELETE_EFFECT INT_VAR match_power = 1 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 20 power = 1 target = 1 parameter2 = 1 timing = 0 duration = 30 resist_dispel = 3 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 92 power = 1 target = 1 parameter1 = 25 timing = 0 duration = 30 resist_dispel = 3 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 59 power = 1 target = 1 parameter1 = 25 timing = 0 duration = 30 resist_dispel = 3 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 275 power = 1 target = 1 parameter1 = 25 timing = 0 duration = 30 resist_dispel = 3 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 power = 1 target = 1 parameter2 = 0 timing = 1 STR_VAR resource = ~SPWI108x~ END
	LPF ALTER_SPELL_HEADER INT_VAR target = 5 END
	SAY NAME1 @6411
	SAY UNIDENTIFIED_DESC @6412
END 													// end sr is installed 
//__________________________________________________________________________________


CLEAR_ARRAYS 
