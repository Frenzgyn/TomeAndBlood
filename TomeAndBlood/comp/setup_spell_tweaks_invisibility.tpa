// -----------------------------
// SPELL TWEAKS: INVISIBILITY REVISIONS
// -----------------------------

// This component uses code by Subtledoctor and is used with permission of the original author. 


//change to invisibility/detect invisible___________________________________________
//
INCLUDE ~%MOD_FOLDER%/lib/d5_insanctibility.tpa~

ACTION_IF NOT FILE_EXISTS_IN_GAME ~d5_insanctibility.d5~ BEGIN
  LAM D5_INSANCTIBILITY
END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN 	// no need to patch 203 if SR installed
  COPY_EXISTING ~spwi203.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 193 target = 1 STR_VAR resource = ~~ END
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 136 duration = 1 END
END

/*
  COPY_EXISTING ~spwi224.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 116 opcode = 136 duration = 1 END 
*/
//__________________________________________________________________________________


//change SR Expeditious Retreat to Chameleon___________________________________________
//
ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN 	// version for Spell Revisions
 COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
	LPF ADD_SPELL_CFEFFECT INT_VAR opcode = 321 target = 1 timing = 1 STR_VAR resource = ~SPWI108~ END
 BUT_ONLY

 COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	LPF ADD_ITEM_EFFECT INT_VAR type = 99 opcode = 321 target = 1 timing = 1 STR_VAR resource = ~SPWI108~ END
  END
 BUT_ONLY

 COPY_EXISTING ~spwi108.spl~ ~override/spwi108x.spl~
	LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
	LPF DELETE_EFFECT INT_VAR match_power = 1 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 21 power = 1 target = 1 parameter1 = 1 timing = 1 duration = 0 resist_dispel = 0 END
	LPF ALTER_SPELL_HEADER INT_VAR speed = 1 END
	WRITE_BYTE 0x19 (THIS BOR 0b00000100) // set "hostile" flag
	SAY NAME1 ~~
	SAY UNIDENTIFIED_DESC ~~

 COPY_EXISTING ~spwi108.spl~ ~override~
	WRITE_BYTE 0x1f (THIS BAND 0b11011111) 				// don't forbid transmuters
	WRITE_BYTE 0x1f (THIS BOR 0b00010000) 				// forbid necromancers
	WRITE_BYTE 0x25 5 									// illusion school
	WRITE_SHORT 0x22 13 								// illusion animation
	WRITE_ASCII 0x10 ~CAS_M01~ #8 						// illusion sounds
	LPF DELETE_EFFECT INT_VAR match_opcode = 321 END
	LPF DELETE_EFFECT INT_VAR match_power = 1 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 20 power = 1 target = 1 parameter2 = 1 timing = 0 duration = 30 resist_dispel = 3 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 92 power = 1 target = 1 parameter1 = 25 timing = 0 duration = 30 resist_dispel = 3 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 59 power = 1 target = 1 parameter1 = 25 timing = 0 duration = 30 resist_dispel = 3 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 275 power = 1 target = 1 parameter1 = 25 timing = 0 duration = 30 resist_dispel = 3 END
	LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 power = 1 target = 1 parameter2 = 0 timing = 1 STR_VAR resource = ~SPWI108x~ END
	LPF ALTER_SPELL_HEADER INT_VAR target = 5 END
	SAY NAME1 @6411
	SAY UNIDENTIFIED_DESC @6412
END 													// end sr is installed 
//__________________________________________________________________________________


//true sight = illusionary creature immunity________________________________________
//
// NO! make this cause a huge luck penalty aura to illusionary creatures

ACTION_IF FILE_EXISTS_IN_GAME ~sppr505.spl~ BEGIN
  COPY_EXISTING ~sppr505.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 193 opcode = 100 parameter2 = 7 parameter1 = 7 END
  BUT_ONLY
END
ACTION_IF FILE_EXISTS_IN_GAME ~spwi609.spl~ BEGIN
  COPY_EXISTING ~spwi609.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 193 opcode = 100 parameter2 = 7 parameter1 = 7 END
  BUT_ONLY
END
ACTION_IF FILE_EXISTS_IN_GAME ~spcl232.spl~ BEGIN
  COPY_EXISTING ~spcl232.spl~ ~override~
	LPF CLONE_EFFECT INT_VAR silent = 1 multi_match = 1 match_opcode = 193 opcode = 100 parameter2 = 7 parameter1 = 7 END
  BUT_ONLY
END
//__________________________________________________________________________________


CLEAR_ARRAYS 
