//This file includes a list of minor changes that are necessary for components in Tome and Blood to work properly.
//No mechanical changes have been made.

// CHANGES
//-----------------------------------
// Generate clab files for Specialists.
// Removing the mage unusability flag from most weapons and armor. 
// Updating spells that prevent casting to use opcode 144 (disable button) instead of 145 (disable spellcasting). 
//------------------------------------

ACTION_IF (NOT FILE_EXISTS_IN_GAME ~qdtnb_core.qd~) THEN BEGIN 
	//Define undefined mage clabs
	ACTION_FOR_EACH kitclab IN ~CLABMA01~ ~CLABMA02~ ~CLABMA03~ ~CLABMA04~ ~CLABMA05~ ~CLABMA06~ ~CLABMA07~ ~CLABMA08~ ~CLABMA09~ BEGIN 
		ACTION_IF (NOT FILE_EXISTS_IN_GAME ~%kitclab%.2DA~) THEN BEGIN
			COPY ~TomeAndBlood/data/core/BLANKCLAB.2da~ ~override/%kitclab%.2da~ //generate undefined clab files
		END 
	END  

	//Allow Mages and Sorcerers to equip any weapon/armor/shield/helmet (must still meet class/race/ability/alignment reqs)
	COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
			READ_SHORT 0x31 "prof" ELSE 0
			READ_SHORT 0x1c "type" ELSE 0
			PATCH_IF ("%prof%" = 89 || //bastard sword
					  "%prof%" = 90 || //long sword
					  "%prof%" = 91 || //short sword
					  "%prof%" = 92 || //axe
					  "%prof%" = 93 || //2h sword
					  "%prof%" = 94 || //katana
					  "%prof%" = 95 || //scimitar 
					  "%prof%" = 97 || //war hammer
					  "%prof%" = 98 || //spear
					  "%prof%" = 99 || //halberd
					  "%prof%" = 100 || //flail/morningstar
					  "%prof%" = 101 || //mace
					  "%prof%" = 103 || //xbow
					  "%prof%" = 104 || //long bow
					  "%prof%" = 105 || //shortbow
					  "%prof%" = 115 || //club
					  "%type%" = 31 || //xbow bolts
					  "%type%" = 5  || //arrows
					  "%type%" = 2  || //armor
					  "%type%" = 7  || //helms
					  "%type%" = 12) BEGIN //shields
						READ_BYTE 0x1e "bard"
						READ_BYTE 0x1f "fighter"
						PATCH_IF (("%fighter%" BAND "0b00001000") = "0b00000000") BEGIN 
							READ_BYTE   0x20 "mage"
							WRITE_BYTE 0x20 ("%mage%" BAND "0b11111011")
							WRITE_BYTE 0x1e ("%bard%" BAND "0b10111111")
						END	
			END
		END
	BUT_ONLY
		
	//new foundation for armored casting

	COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
	  PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		READ_SHORT 0x1c type
		READ_LONG 0x22 appearance
		PATCH_IF (type = 2) BEGIN // armor
		  PATCH_IF (appearance = 16690) BEGIN // leather appearance
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 145 opcode = 177 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~d5arcal~ END
		  END
		  PATCH_IF (appearance = 16691) BEGIN // chain appearance
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 145 opcode = 177 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~d5arcac~ END
		  END
		  PATCH_IF (appearance = 16692) BEGIN // plate appearance
			LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 145 opcode = 177 parameter1 = 0 parameter2 = 2 STR_VAR resource = ~d5arcap~ END
		  END
		END
	  END
	BUT_ONLY
	COPY ~TomeAndBlood/data/core/d5arcal.eff~ ~override~
	COPY ~TomeAndBlood/data/core/d5arcac.eff~ ~override~
	COPY ~TomeAndBlood/data/core/d5arcap.eff~ ~override~
	COPY ~TomeAndBlood/data/core/d5arca1.spl~ ~override~
	COPY ~TomeAndBlood/data/core/d5arca2.spl~ ~override~
	COPY ~TomeAndBlood/data/core/d5arca3.spl~ ~override~
	COPY ~TomeAndBlood/data/core/d5arca1.eff~ ~override~
	COPY ~TomeAndBlood/data/core/d5arca2.eff~ ~override~
	COPY ~TomeAndBlood/data/core/d5arca3.eff~ ~override~

	//Update following spells to use 144 instead of 145 -> allows for armored casting components later. 
	//SPPR507 Champion's Strength
	//SPSD02 Shadowstep
	//SPWI315 Wraithform
	//SWI603 Tenser's Transformations
	//SPWI711 Sphere of Chaos 

	ACTION_FOR_EACH spl IN ~sppr507~ ~spsd02~ ~spwi315~ ~spwi603~ ~spwi711~ BEGIN 
		ACTION_IF (FILE_EXISTS_IN_GAME ~%spl%.spl~) THEN BEGIN
			COPY_EXISTING ~%spl%.spl~ ~override~ 
				LPF ALTER_SPELL_EFFECT INT_VAR match_opcode = 145 new_opcode = 144 parameter2 = 2  END
		END 
	END 	
	
	/*Adding animations for various martial casters*/
	/*Animations courtesy of Grammarsalad and Dib and used with permission*/
	COPY ~TomeAndBlood/data/anims/B_ANIFS.spl~ ~override~ //Animation spl
		 ~TomeAndBlood/data/anims/B_AFSFH.eff~ ~override~ //Female Human
		 ~TomeAndBlood/data/anims/B_FSFH.eff~ ~override~ //Female Human
		 ~TomeAndBlood/data/anims/B_AFSFE.eff~ ~override~ //Female Elf
		 ~TomeAndBlood/data/anims/B_FSFE.eff~ ~override~ //Female Elf
		 ~TomeAndBlood/data/anims/B_AFSFHE.eff~ ~override~ //Female hElf--ani as elf
		 ~TomeAndBlood/data/anims/B_AFSFD.eff~ ~override~ //Female Dwarf
		 ~TomeAndBlood/data/anims/B_FSFD.eff~ ~override~ //Female Dwarf
		 ~TomeAndBlood/data/anims/B_AFSFHA.eff~ ~override~ //Female Half
		 ~TomeAndBlood/data/anims/B_FSFHA.eff~ ~override~ //Female Half
		 ~TomeAndBlood/data/anims/B_AFSFG.eff~ ~override~ //Female Gnome
		 ~TomeAndBlood/data/anims/B_FSFG.eff~ ~override~ //Female Gnome
		 ~TomeAndBlood/data/anims/B_AFSFHO.eff~ ~override~ //Female Halforc
		 ~TomeAndBlood/data/anims/B_FSFHO.eff~ ~override~ //Female Halforc
		 ~TomeAndBlood/data/anims/B_AFSMH.eff~ ~override~ //Male Human
		 ~TomeAndBlood/data/anims/B_FSMH.eff~ ~override~ //Male Human
		 ~TomeAndBlood/data/anims/B_AFSME.eff~ ~override~ //male Elf
		 ~TomeAndBlood/data/anims/B_FSME.eff~ ~override~ //male Elf
		 ~TomeAndBlood/data/anims/B_AFSMHE.eff~ ~override~ //male hElf--ani as elf
		 ~TomeAndBlood/data/anims/B_AFSMD.eff~ ~override~ //male Dwarf
		 ~TomeAndBlood/data/anims/B_FSMD.eff~ ~override~ //male Dwarf
		 ~TomeAndBlood/data/anims/B_AFSMHA.eff~ ~override~ //male Half
		 ~TomeAndBlood/data/anims/B_FSMHA.eff~ ~override~ //male Half
		 ~TomeAndBlood/data/anims/B_AFSMG.eff~ ~override~ //male Gnome
		 ~TomeAndBlood/data/anims/B_FSMG.eff~ ~override~ //male Gnome
		 ~TomeAndBlood/data/anims/B_AFSMHO.eff~ ~override~ //male Halforc
		 ~TomeAndBlood/data/anims/B_FSMHO.eff~ ~override~ //male Halforc
		 ~TomeAndBlood/data/anims/D#FAVANI.BCS~ ~override~ //appearance script
		 ~TomeAndBlood/data/anims/D#FAVANI.SPL~ ~override~ //appearance spell
	
	
	//Add .2da map matching arcane spells to scrolls
		
  ACTION_IF NOT FILE_EXISTS_IN_GAME ~qdscrlmap.2da~ BEGIN

<<<<<<<< QD/qdscrlmap.2da
2DA V1.0 
SPLRES
			ITMRES
>>>>>>>> 

	COPY ~QD/qdscrlmap.2da~ ~override/qdscrlmap.2da~ 

	COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~ 
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN 
			GET_OFFSET_ARRAY headers ITM_V10_HEADERS
			PHP_EACH headers AS int => hoff BEGIN 
				GET_OFFSET_ARRAY2 fx hoff ITM_V10_HEAD_EFFECTS
				PHP_EACH fx AS intj => foff BEGIN 
					READ_SHORT foff opcode 
					PATCH_IF (opcode = 147) BEGIN 
						READ_ASCII (foff + 0x14) splres
						PHP_EACH arcanespell AS spl => schl BEGIN 
							PATCH_IF ~%spl%~ STRING_EQUAL_CASE ~%splres%~ BEGIN
								TEXT_SPRINT splschool ~%schl%~
							END
						END
						//PATCH_PRINT ~%SOURCE_RES%  => %splres% ~
						INNER_ACTION BEGIN 
							APPEND ~qdscrlmap.2da~ ~%splres%	%SOURCE_RES%	%splschool% ~
						END 
					END 
				END 
			END 
		END 
	BUT_ONLY
	
	COPY_EXISTING ~qdscrlmap.2da~ ~override~
		PRETTY_PRINT_2DA

  END
	
	
  //check file

  COPY_EXISTING ~sw1h01.itm~ ~override/qdtnb_core.qd~
	
END 