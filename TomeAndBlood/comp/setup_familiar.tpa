// -----------------------------
// INNATE FAMILIAR
// -----------------------------

INCLUDE ~TomeAndBlood/data/core/tnb_kit_list.tpa~ 

//Add Find Familiar innate to mages

ACTION_PHP_EACH tnb_kit_list AS kitinfo => kitclab BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%kitclab%.2da~) BEGIN 
		APPEND ~%kitclab%.2da~ ~FAMILIAR 	GA_SPCL342~
	END 
END  

//Remove Find Familiar from Spell Selection menu
APPEND ~HIDESPL.2da~ ~SPWI123	1	0~ 

//Replace Scrolls of Find Familiar with Scrolls of Magic Missile. 
COPY_EXISTING ~scrl77.itm~ ~override/scrl6d.itm~ 

//Update descriptions of mage/sorcerer
COPY_EXISTING - ~clastext.2da~ ~override~
		COUNT_2DA_COLS num_cols
			READ_2DA_ENTRIES_NOW ~r2en_clastext~ num_cols
			FOR (row = 1; row < r2en_clastext; row += 1) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_clastext~ row 0 kitname
				READ_2DA_ENTRY_FORMER ~r2en_clastext~ row 4 desc
				PATCH_IF ((~%kitname%~ STRING_EQUAL_CASE ~MAGE~) OR 
						  (~%kitname%~ STRING_EQUAL_CASE ~SORCERER~)) BEGIN
					INNER_ACTION  BEGIN 
						ACTION_IF (desc >= 0) BEGIN
							ACTION_GET_STRREF desc oldstring
							OUTER_SPRINT old @1001
							OUTER_SPRINT new @1002
							OUTER_PATCH_SAVE oldstring ~%oldstring%~ BEGIN
								REPLACE_TEXTUALLY ~%old%~ ~%new%~
							END
							STRING_SET_EVALUATE desc ~%oldstring%~
						END 
					END
				END
			END 