// -----------------------------
// INNATE FAMILIAR
// -----------------------------

DEFINE_ACTION_FUNCTION innate_familiars BEGIN 

INCLUDE ~TomeAndBlood/data/core/tnb_kit_list.tpa~ 

//Add Find Familiar innate to mages

COPY ~tomeandblood/lib/d5_base.spl~ ~override/d5fminn.spl~
  WRITE_ASCII 0x3a ~spwi123c~ 
  LPF ALTER_SPELL_HEADER STR_VAR icon = ~spwi123b~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 171 target = 1 timing = 1 STR_VAR resource = ~spcl342~ END

ACTION_PHP_EACH tnb_kit_list AS kitinfo => kitclab BEGIN 
	ACTION_IF (FILE_EXISTS_IN_GAME ~%kitclab%.2da~) BEGIN 
		APPEND ~%kitclab%.2da~ ~FAMILIAR 	AP_D5FMINN  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****    ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        **** ~
	END 
END  

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_ROWS 9 rows
  FOR (row = 2 ; row < rows ; ++row) BEGIN
	READ_2DA_ENTRY row 1 9 modname
	READ_2DA_ENTRY row 5 9 modclab
	READ_2DA_ENTRY row 8 9 modclass
	PATCH_IF (modclass = 1) OR (modclass = 19) BEGIN
	  PATCH_IF !(%modname% STRING_MATCHES_REGEXP ~C0SA+~ = 0) BEGIN
		INNER_ACTION BEGIN
		  APPEND ~%modclab%.2da~ ~FAMILIAR 	AP_D5FMINN  ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****    ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        **** ~ UNLESS ~FAMILIAR~
		END
	  END
	END
  END
BUT_ONLY

//Remove Find Familiar from Spell Selection menu
ACTION_IF (FILE_EXISTS_IN_GAME ~hidespl.2da~) AND (GAME_IS ~iwdee~) BEGIN
	APPEND ~hidespl.2da~ ~SPWI123	1		0~
END
ACTION_IF (FILE_EXISTS_IN_GAME ~hidespl.2da~) AND !(GAME_IS ~iwdee~) BEGIN
	APPEND ~hidespl.2da~ ~SPWI123	1		0		0~
END

//Replace Scrolls of Find Familiar with Scrolls of Magic Missile. 
COPY_EXISTING ~scrl77.itm~ ~override/scrl6d.itm~ 

//Update descriptions of mage/sorcerer
COPY_EXISTING - ~clastext.2da~ ~override~
		COUNT_2DA_COLS num_cols
			READ_2DA_ENTRIES_NOW ~r2en_clastext~ num_cols
			FOR (row = 1; row < r2en_clastext; row += 1) BEGIN
				READ_2DA_ENTRY_FORMER ~r2en_clastext~ row 0 kitname
				READ_2DA_ENTRY_FORMER ~r2en_clastext~ row 4 desc
				PATCH_IF ((~%kitname%~ STRING_EQUAL_CASE ~MAGE~) OR 
						  (~%kitname%~ STRING_EQUAL_CASE ~SORCERER~)) BEGIN
					INNER_ACTION  BEGIN 
						ACTION_IF (desc >= 0) BEGIN
							ACTION_GET_STRREF desc oldstring
							OUTER_SPRINT old @1001
							OUTER_SPRINT new @1002
							OUTER_PATCH_SAVE oldstring ~%oldstring%~ BEGIN
								REPLACE_TEXTUALLY ~%old%~ ~%new%~
							END
							STRING_SET_EVALUATE desc ~%oldstring%~
						END 
					END
				END
			END 

LAM JOINABLE_NPC_ARRAYS

ACTION_PHP_EACH JOINABLE_NPC_ARRAY AS cre => dv BEGIN
  COPY_EXISTING ~%cre%~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x2d3) BEGIN
	  READ_BYTE 0x273 npc_class
	  PATCH_IF (npc_class = 1) || (npc_class = 5) || (npc_class = 7) || (npc_class = 10) || (npc_class = 13) || (npc_class = 14) || (npc_class = 17) || (npc_class = 19) BEGIN
		LPF ADD_CRE_EFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 9 STR_VAR resource = ~d5fminn~ END
		LPF ADD_CRE_EFFECT INT_VAR opcode = 321 target = 1 timing = 9 STR_VAR resource = ~d5fminn~ END
	  END
	END
  IF_EXISTS BUT_ONLY
END
	
END 	//	end define function
//____________________________________________________________________________________



////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////


